apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        release {
            storeFile file(keystoreProperties["keystoreFile"])
            storePassword keystoreProperties["keystorePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]
        }

        debug {

            storeFile file(keystoreProperties["keystoreFile"])
            storePassword keystoreProperties["keystorePassword"]
            keyAlias keystoreProperties["keyAlias"]
            keyPassword keystoreProperties["keyPassword"]

        }

    }

    defaultConfig {
        applicationId "com.winsun.fruitmix"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

    }
    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug

            buildConfigField("String", "server_url", "\"test.siyouqun.com\"")
            buildConfigField("int", "server_port", "4000")

            buildConfigField("String", "mqtt_server_url", "\"tcp://test.siyouqun.com\"")

        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField("String", "server_url", "\"www.siyouqun.com\"")
            buildConfigField("int", "server_port", "80")

            buildConfigField("String", "mqtt_server_url", "\"tcp://mqtt.siyouqun.com\"")

        }
        releaseTest {

            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField("String", "server_url", "\"test.siyouqun.com\"")
            buildConfigField("int", "server_port", "4000")

            buildConfigField("String", "mqtt_server_url", "\"tcp://test.siyouqun.com\"")

        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
        maven {
            url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
        }
    }

    dataBinding {
        enabled = true
    }


    kapt {
        arguments {
            arg('eventBusIndex', 'com.wisnun.fruitmix.MyEventBusIndex')
        }
    }

    sourceSets {
        debug {
            manifest.srcFile 'src/debug/AndroidManifest.xml'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {

            if (variant.buildType.name.equals('release')) {
                outputFileName = "wisnuc-v${defaultConfig.versionName}-release.apk"
            } else if (variant.buildType.name.equals('releaseTest')) {
                outputFileName = "wisnuc-v${defaultConfig.versionName}-release-test-cloud.apk"
            } else if (variant.buildType.name.equals('debug')) {
                outputFileName = "wisnuc-v${defaultConfig.versionName}-debug.apk"
            }

        }
    }
}

configurations.all {
    resolutionStrategy {
        force rootProject.ext.supportAnnotation
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api project(':volley')
    implementation rootProject.ext.design
    implementation rootProject.ext.cardviewV7
    implementation rootProject.ext.recyclerviewV7
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.rulesVersion"
    androidTestImplementation "junit:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Resolve conflicts between main and test APK:
    androidTestImplementation rootProject.ext.supportV4
    androidTestImplementation rootProject.ext.recyclerviewV7
    androidTestImplementation rootProject.ext.appcompatV7
    androidTestImplementation rootProject.ext.design
    androidTestImplementation rootProject.ext.cardviewV7
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttpVersion"
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.8'
    implementation 'me.relex:circleindicator:1.2.2@aar'
    implementation 'io.github.sin3hz.fastjumper:fastjumper:0.1.0'
    implementation 'com.daimajia.swipelayout:library:1.2.0@aar'
    implementation 'com.github.andriydruk:rxdnssd:0.9.1'
    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:1.3.4'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation project(':steppers')
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    implementation 'com.szysky.customize:simageview:2.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    debugImplementation('com.facebook.stetho:stetho:1.5.0') {
        exclude module: 'jsr305'
    }

    implementation 'org.greenrobot:eventbus:3.1.1'
    kapt 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    kapt "com.android.databinding:compiler:$android_plugin_version"


}
repositories {
    mavenCentral()
}
